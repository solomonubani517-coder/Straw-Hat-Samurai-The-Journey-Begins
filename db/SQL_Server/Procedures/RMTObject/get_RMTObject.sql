/*
** Copyright 2025 Metaversal Corporation.
** 
** Licensed under the Apache License, Version 2.0 (the "License"); 
** you may not use this file except in compliance with the License. 
** You may obtain a copy of the License at 
** 
**    https://www.apache.org/licenses/LICENSE-2.0
** 
** Unless required by applicable law or agreed to in writing, software 
** distributed under the License is distributed on an "AS IS" BASIS, 
** WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
** See the License for the specific language governing permissions and 
** limitations under the License.
** 
** SPDX-License-Identifier: Apache-2.0
*/

/******************************************************************************************************************************/

/*
*/

DROP PROCEDURE IF EXISTS dbo.get_RMTObject
GO

CREATE PROCEDURE dbo.get_RMTObject
(
   @sIPAddress                   NVARCHAR (16),
   @twRMTObjectIx                BIGINT
)
AS
BEGIN
           SET NOCOUNT ON

       DECLARE @SBO_CLASS_RMTOBJECT                       INT = 72
       DECLARE @MVO_RMTOBJECT_TYPE_PARCEL                 INT = 11

            -- Create the temp Error table
        SELECT * INTO #Error FROM dbo.Table_Error ()

       DECLARE @bError  INT = 0,
               @bCommit INT = 0,
               @nError  INT

       DECLARE @bType TINYINT

            -- Create the temp Results table
        SELECT * INTO #Results FROM dbo.Table_Results ()

            -- validate input

        SELECT @bType = Type_bType
          FROM dbo.RMTObject
         WHERE ObjectHead_Self_twObjectIx = @twRMTObjectIx

            IF @bType IS NULL
               SET @bError = 1

            IF (@bError = 0)
         BEGIN
               INSERT #Results
               SELECT 0,
                      ObjectHead_Self_twObjectIx
                 FROM dbo.RMTObject
                WHERE ObjectHead_Self_twObjectIx = @twRMTObjectIx

                   IF @bType <> @MVO_RMTOBJECT_TYPE_PARCEL
                BEGIN
                      INSERT #Results
                      SELECT 1,
                             ObjectHead_Self_twObjectIx
                        FROM dbo.RMTObject
                       WHERE ObjectHead_Parent_wClass     = @SBO_CLASS_RMTOBJECT
                         AND ObjectHead_Parent_twObjectIx = @twRMTObjectIx
                  END
                 ELSE
                BEGIN
                      INSERT #Results
                      SELECT 1,
                             ObjectHead_Self_twObjectIx
                        FROM dbo.RMPObject
                       WHERE ObjectHead_Parent_wClass     = @SBO_CLASS_RMTOBJECT
                         AND ObjectHead_Parent_twObjectIx = @twRMTObjectIx
                  END

                 EXEC dbo.call_RMTObject_Select 0
                   IF @bType <> @MVO_RMTOBJECT_TYPE_PARCEL
                      EXEC dbo.call_RMTObject_Select 1
                 ELSE EXEC dbo.call_RMPObject_Select 1

                  SET @bCommit = 1
           END

        RETURN @bCommit - @bError - 1
  END
GO

GRANT EXECUTE ON dbo.get_RMTObject TO WebService
GO

/******************************************************************************************************************************/
